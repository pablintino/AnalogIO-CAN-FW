function(add_jlink_flash_target EXECUTABLE)
    set(TARGET_DEVICE "${DEVICE}")
    get_filename_component(EXEC_NAME ${EXECUTABLE} NAME_WE)
    set(TARGET_EXECUTABLE_HEX "${EXEC_NAME}.hex")
    set(TEMPLATE_FLASH_JLINK_PATH ${CMAKE_SOURCE_DIR}/utilities/flash_script.jlink.in)
    set(JLINK_COMMANDER_EXEC "JLinkExe")
    find_program(JLINK_COMMANDER_EXEC_PATH ${JLINK_COMMANDER_EXEC})
    if(JLINK_COMMANDER_EXEC_PATH)
        if(NOT EXISTS ${TEMPLATE_FLASH_JLINK_PATH})
            message(STATUS "JLink flash template doesn't exist. Flash target will not be added")
        else()
            configure_file(${TEMPLATE_FLASH_JLINK_PATH} ${CMAKE_CURRENT_BINARY_DIR}/flash_script_final.jlink @ONLY)
            add_custom_target(flash DEPENDS build-hex COMMAND ${JLINK_COMMANDER_EXEC_PATH} -CommandFile ${CMAKE_CURRENT_BINARY_DIR}/flash_script_final.jlink)
        endif()
    else()
        message(STATUS "Cannot find JLinkExe (commander). Flash target will not be added")
    endif()
endfunction()
